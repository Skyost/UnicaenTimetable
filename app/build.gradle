apply plugin: 'com.android.application'

configurations {
    compile.exclude group: 'commons-logging' // Referenced in some portable lib. use android internal instead.
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    packagingOptions {
        exclude('org/apache/commons/codec/language/bm/*.txt') // 124 files (uncompressed 215kb) with unused language specific rules.
    }
    defaultConfig {
        applicationId 'fr.skyost.timetable'
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 62
        versionName 'v2.2.4'
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/schemas".toString()]
            }
        }
        resValue "string", "ADMOB_APP_ID", getLocalProperty("admob.appId")
        resValue "string", "ADMOB_HOME_SCREEN_BANNER", getLocalProperty("admob.homeScreenBanner")
        resConfigs "en", "fr"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'account_type_authority', '"fr.skyost.timetable"'
        }
        debug {
            applicationIdSuffix = '.dev'
            versionNameSuffix '-dev'
            resValue 'string', 'account_type_authority', '"fr.skyost.timetable.dev"'
            resValue "string", "ADMOB_HOME_SCREEN_BANNER", "ca-app-pub-3940256099942544/6300978111"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.1.0'

    implementation 'androidx.room:room-runtime:2.2.0-rc01'
    annotationProcessor 'androidx.room:room-compiler:2.2.0-rc01'
    implementation 'org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.1.0'

    implementation 'net.sf.biweekly:biweekly:0.6.3'
    implementation 'com.github.AppIntro:AppIntro:v5.1.0'
    implementation 'io.github.kobakei:ratethisapp:1.2.0'
    implementation 'joda-time:joda-time:2.10.3'
    implementation project(':android-week-view')
    //implementation 'com.github.thellmund:Android-Week-View:3.4.2'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.github.matecode:Snacky:1.1.4'
    implementation 'com.google.android.gms:play-services-ads:18.2.0'
}

def getLocalProperty(String name) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty(name)
}